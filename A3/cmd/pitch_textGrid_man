if numberOfSelected ("Sound") <> 1 or numberOfSelected ("TextGrid") <> 1
    exitScript: "Please select a Sound and a TextGrid first."
endif
sound = selected ("Sound")
textgrid = selected ("TextGrid")
writeInfoLine: "Result:"
selectObject: sound
To Pitch: 0.0, 75, 600
pitch = selected ("Pitch")
selectObject: textgrid
n = Get number of intervals: 1
for i to n
    tekst$ = Get label of interval: 1, i
    if tekst$ <> ""
       t1 = Get starting point: 1, i
       t2 = Get end point: 1, i
       selectObject: pitch
       f0 = Get mean: t1, t2, "Hertz"
       appendInfoLine: fixed$ (t1, 3), " ", fixed$ (t2, 3), " ", round (f0), " ", tekst$
       selectObject: textgrid
    endif
endfor
selectObject: sound, textgrid